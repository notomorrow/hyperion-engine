#version 450

#extension GL_GOOGLE_include_directive : require
#extension GL_EXT_scalar_block_layout : require
#extension GL_EXT_nonuniform_qualifier : require

layout(local_size_x = 8, local_size_y = 8, local_size_z = 1) in;

#include "../include/defines.inc"
#include "../include/shared.inc"
#include "../include/noise.inc"
#include "../include/packing.inc"

#define HYP_DO_NOT_DEFINE_DESCRIPTOR_SETS
#include "../include/scene.inc"
#undef HYP_DO_NOT_DEFINE_DESCRIPTOR_SETS

layout(set = 0, binding = 0) uniform texture2D gbuffer_albedo_texture;
layout(set = 0, binding = 1) uniform texture2D gbuffer_normals_texture;
layout(set = 0, binding = 2) uniform texture2D gbuffer_depth_texture;
layout(set = 0, binding = 3) uniform texture2D gbuffer_velocity_texture;
layout(set = 0, binding = 4) uniform sampler sampler_nearest;
layout(set = 0, binding = 5) uniform sampler sampler_linear;

layout(std140, set = 0, binding = 6, row_major) readonly buffer SceneBuffer
{
    Scene scene;
};

layout(set = 0, binding = 7, rgba8) uniform image2D output_image;
layout(set = 0, binding = 8) uniform texture2D prev_image;

layout(push_constant) uniform PushConstant
{
    uvec2 dimension;
    float temporal_blending_factor;
};

#include "../include/Temporal.glsl"


#define HYP_HBAO_NUM_CIRCLES 4
#define HYP_HBAO_NUM_SLICES 2
#define HYP_HBAO_RADIUS 64.0
#define HYP_HBAO_POWER 1.0

float fov_rad = HYP_FMATH_DEG2RAD(scene.camera_fov);
float tan_half_fov = tan(fov_rad * 0.5);
float inv_tan_half_fov = 1.0 / tan_half_fov;
vec2 focal_len = vec2(inv_tan_half_fov * (float(scene.resolution_y) / float(scene.resolution_x)), inv_tan_half_fov);
vec2 inv_focal_len = vec2(1.0) / focal_len;
vec4 uv_to_view = vec4(2.0 * inv_focal_len.x, 2.0 * inv_focal_len.y, -1.0 * inv_focal_len.x, -1.0 * inv_focal_len.y);

float GetOffsets(vec2 uv)
{
    ivec2 position = ivec2(uv * vec2(dimension));
    return 0.25 * float((position.y - position.x) & 3);
}

mat4 inv_view_proj = inverse(scene.projection * scene.view);
mat4 inv_proj = inverse(scene.projection);

float GetDepth(vec2 uv)
{
    return Texture2DLod(sampler_nearest, gbuffer_depth_texture, uv, 0.0).r;
}

vec3 GetPosition(vec2 uv, float depth)
{
    return ReconstructViewSpacePositionFromDepth(inv_proj, uv, depth).xyz;
    // const float linear_depth = ViewDepth(depth, scene.camera_near, scene.camera_far);
    // return vec3((vec2(uv.x, 1.0 - uv.y) * uv_to_view.xy + uv_to_view.zw) * linear_depth, linear_depth);
}

vec3 GetNormal(vec2 uv)
{
    vec3 normal = DecodeNormal(Texture2DLod(sampler_nearest, gbuffer_normals_texture, uv, 0.0));
    vec3 view_normal = (scene.view * vec4(normal, 0.0)).xyz;

    return view_normal;
}

float IntegrateUniformWeight(vec2 h)
{
    vec2 arc = vec2(1.0) - cos(h);

    return arc.x + arc.y;
}

float IntegrateArcCosWeight(vec2 h, float n)
{
    vec2 arc = -cos(2.0 * h - n) + cos(n) + 2.0 * h * sin(n);

    return 0.25 * (arc.x + arc.y);
}

vec2 RotateDirection(vec2 uv, vec2 cos_sin)
{
    return vec2(
        uv.x * cos_sin.x - uv.y * cos_sin.y,
        uv.x * cos_sin.y + uv.y * cos_sin.x
    );
}

#define ANGLE_BIAS 0.2

vec3 GetCameraSpaceRay(in vec3 V, in vec2 ray)
{
    return normalize(vec3(ray * -V.z, dot(V.xy, ray)));
}

void TraceAO_New(vec2 uv, out float occlusion, out vec3 bent_normal, out vec4 light_color)
{
    uvec2 pixel_coord = clamp(uvec2(uv * vec2(dimension) - 0.5), uvec2(0), dimension - 1);
    uint seed = InitRandomSeed(InitRandomSeed(pixel_coord.x, pixel_coord.y), scene.frame_counter);

    const float projected_scale = float(dimension.y) / (tan_half_fov * 2.0);

    const float temporal_offset = GetSpatialOffset(scene.frame_counter);
    const float temporal_rotation = GetTemporalRotation(scene.frame_counter);

    const float noise_offset = GetOffsets(uv);
    const float noise_direction = InterleavedGradientNoise(vec2(pixel_coord));
    const float ray_step = fract(noise_offset + temporal_offset);

    const float depth = GetDepth(uv);
    const vec3 P = GetPosition(uv, depth);
    const vec3 N = GetNormal(uv);
    const vec3 V = normalize(vec3(0.0) - P);

    const float camera_distance = P.z;
    const vec2 texel_size = vec2(1.0) / vec2(dimension);
    const float step_radius = max((projected_scale * HYP_HBAO_RADIUS) / max(camera_distance, HYP_FMATH_EPSILON), float(HYP_HBAO_NUM_SLICES)) / float(HYP_HBAO_NUM_SLICES + 1);

    occlusion = 0.0;
    bent_normal = vec3(0.0);
    light_color = vec4(0.0);

    for (int i = 0; i < HYP_HBAO_NUM_CIRCLES; i++) {
        float angle = (float(i) + noise_direction + ((temporal_rotation / 360.0))) * (HYP_FMATH_PI / float(HYP_HBAO_NUM_CIRCLES)) * 2.0;

        vec2 ss_ray = vec2(cos(angle), sin(angle));
        vec2 horizons = vec2(-1.0);

        vec2 slice_ao = vec2(0.0);
        vec4 slice_light[2] = { vec4(0.0), vec4(0.0) };

        for (int j = 0; j < HYP_HBAO_NUM_SLICES; j++) {
            vec2 uv_offset = (ss_ray * texel_size) * max(step_radius * (float(j) + ray_step), float(j + 1));
            // uv_offset *= mix(-1.0, 1.0, float(j % 2 == 0));

            vec4 new_uv = uv.xyxy + vec4(uv_offset, -uv_offset);

            // Fade hits that approach the screen edge
            const vec4 new_uv_ndc = new_uv * 2.0 - 1.0;
            const float max_dimension = min(1.0, max(abs(new_uv_ndc.x), abs(new_uv_ndc.y)));
            const float fade = 1.0;// - max(0.0, max_dimension - 0.75) / (1.0 - 0.98);

            if (new_uv.xy == Saturate(new_uv.xy)) {
                new_uv = Saturate(new_uv);

                vec3 ds = GetPosition(new_uv.xy, GetDepth(new_uv.xy)) - P;
                vec3 dt = GetPosition(new_uv.zw, GetDepth(new_uv.zw)) - P;

                const vec2 len = vec2(length(ds), length(dt));
                const vec2 dist = len / HYP_HBAO_RADIUS;
                ds /= len.x;
                dt /= len.y;

                const vec2 DdotD = vec2(dot(ds, ds), dot(dt, dt));
                const vec2 NdotD = vec2(dot(ds, N), dot(dt, N));
                const vec2 VdotD = vec2(dot(ds, V), dot(dt, V));

                const vec2 H = VdotD * inversesqrt(DdotD);

                const vec2 falloff = Saturate(DdotD * (2.0 / HYP_FMATH_SQR(HYP_HBAO_RADIUS)));

                const vec2 color_weights = smoothstep(0.01, 0.4, dist) * fade;
                const vec2 condition = vec2(greaterThan(H - falloff, horizons)) * vec2(lessThan(dist, vec2(0.9995)));

                const vec3 new_color_0 = Texture2DLod(sampler_linear, gbuffer_albedo_texture, new_uv.xy, 0.0).rgb;
                const vec3 new_color_1 = Texture2DLod(sampler_linear, gbuffer_albedo_texture, new_uv.zw, 0.0).rgb;

                slice_light[0] = mix(
                    slice_light[0],
                    slice_light[0] + vec4(new_color_0, 1.0),
                    condition.x * color_weights.x
                );

                slice_light[1] = mix(
                    slice_light[1],
                    slice_light[1] + vec4(new_color_1, 1.0),
                    condition.y * color_weights.y
                );

                slice_ao = mix(
                    slice_ao,
                    slice_ao + vec2(
                        (1.0 - HYP_FMATH_SQR(dist.x)) * (NdotD.x - slice_ao.x),
                        (1.0 - HYP_FMATH_SQR(dist.y)) * (NdotD.y - slice_ao.y)
                    ),
                    condition
                );

                horizons = mix(horizons, mix(H, horizons, falloff), condition);
            }
        }

        light_color += slice_light[0] + slice_light[1];
        occlusion += slice_ao.x + slice_ao.y;
    }

    occlusion = 1.0 - Saturate(pow(occlusion / float(HYP_HBAO_NUM_CIRCLES), 1.0 / HYP_HBAO_POWER));
    occlusion *= 1.0 / (1.0 - ANGLE_BIAS);

    light_color /= max(light_color.a, 1.0);
    light_color *= 1.0 / (1.0 - ANGLE_BIAS);
}

void main()
{
    const uvec2 pixel_coord = gl_GlobalInvocationID.xy;
    
    if (any(greaterThanEqual(pixel_coord, dimension))) {
        return;
    }

    const vec2 uv = (vec2(pixel_coord) + 0.5) / vec2(dimension);

    float occlusion;
    vec3 bent_normal;
    vec4 light_color;

    TraceAO_New(uv, occlusion, bent_normal, light_color);

    // vec4 next_values = vec4(light_color.rgb, occlusion);
    vec4 next_values = vec4(light_color.rgb, occlusion);
    // vec4 next_values = vec4(light_color_2.rgb - light_color.rgb, occlusion_2 - occlusion);
    
    imageStore(output_image, clamp(ivec2(pixel_coord), ivec2(0.0), ivec2(dimension - 1)), next_values);
}