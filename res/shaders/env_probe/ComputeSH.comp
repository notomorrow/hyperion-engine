
#version 450

#extension GL_GOOGLE_include_directive : require

#define NUM_SAMPLES_X 16
#define NUM_SAMPLES_Y 16

#define HYP_SAMPLER_NEAREST sampler_nearest
#define HYP_SAMPLER_LINEAR sampler_linear

struct SHTile
{
    vec4 coeffs_weights[9];
};

// #define PARALLEL_REDUCE

#if defined(MODE_BUILD_COEFFICIENTS) || defined(MODE_CLEAR)
layout(local_size_x = NUM_SAMPLES_X, local_size_y = NUM_SAMPLES_Y) in;
#elif defined(MODE_REDUCE)
layout(local_size_x = 6, local_size_y = 4, local_size_z = 4) in;
#elif defined(MODE_FINALIZE)
layout(local_size_x = 1) in;
#endif

#define HYP_DO_NOT_DEFINE_DESCRIPTOR_SETS

HYP_DESCRIPTOR_SAMPLER(Global, SamplerLinear) uniform sampler sampler_linear;
HYP_DESCRIPTOR_SAMPLER(Global, SamplerNearest) uniform sampler sampler_nearest;

#include "../include/defines.inc"
#include "../include/shared.inc"
#include "../include/tonemap.inc"
#include "../include/packing.inc"
#include "../include/env_probe.inc"
#include "../include/scene.inc"
#include "../include/Octahedron.glsl"

HYP_DESCRIPTOR_SRV(Scene, EnvProbeTextures, count = 16) uniform texture2D env_probe_textures[16];
HYP_DESCRIPTOR_SSBO(Scene, EnvProbesBuffer) buffer EnvProbesBuffer { EnvProbe env_probes[]; };
HYP_DESCRIPTOR_SSBO(Scene, SHGridBuffer) buffer SHGridBuffer { vec4 sh_grid_buffer[SH_GRID_BUFFER_SIZE]; };

HYP_DESCRIPTOR_SSBO(Scene, ShadowMapsBuffer) readonly buffer ShadowMapsBuffer { ShadowMap shadow_map_data[16]; };
HYP_DESCRIPTOR_SRV(Scene, ShadowMapTextures, count = 16) uniform texture2D shadow_maps[16];
HYP_DESCRIPTOR_SRV(Scene, PointLightShadowMapTextures, count = 16) uniform textureCube point_shadow_maps[16];

HYP_DESCRIPTOR_SSBO_DYNAMIC(Scene, CurrentLight) readonly buffer CurrentLight { Light light; };
HYP_DESCRIPTOR_SSBO_DYNAMIC(Scene, CurrentEnvProbe) readonly buffer CurrentEnvProbe { EnvProbe current_env_probe; };

#include "../include/shadows.inc"
#include "../include/Octahedron.glsl"

HYP_DESCRIPTOR_SRV(ComputeSHDescriptorSet, InColorCubemap) uniform textureCube cubemap_color;
HYP_DESCRIPTOR_SRV(ComputeSHDescriptorSet, InNormalsCubemap) uniform textureCube cubemap_normals;
HYP_DESCRIPTOR_SRV(ComputeSHDescriptorSet, InDepthCubemap) uniform textureCube cubemap_depth;

HYP_DESCRIPTOR_SSBO(ComputeSHDescriptorSet, InputSHTilesBuffer) buffer InputSHTilesBuffer { SHTile sh_tiles[]; };
HYP_DESCRIPTOR_SSBO(ComputeSHDescriptorSet, OutputSHTilesBuffer) buffer OutputSHTilesBuffer { SHTile sh_tiles_output[]; };

layout(push_constant) uniform PushConstant
{
    uint    env_probe_index;
    uvec4   probe_grid_position;
    uvec4   cubemap_dimensions;
    uvec4   level_dimensions;
    vec4    world_position;
};

#if defined(MODE_BUILD_COEFFICIENTS) || defined(MODE_CLEAR)
#define CURRENT_TILE sh_tiles[(sample_index.x * NUM_SAMPLES_Y) + sample_index.y]
#endif

vec3 MapXYSToDirection(uint face_index, vec2 uv) {
    vec3 dir = vec3(0.0);

    float u = uv.x;
    float v = uv.y;

    // +x, -x, +y, -y, +z, -z
    switch (face_index) {
    case 0:
        dir = normalize(vec3(1.0, -v, -u));
        break;
    case 1:
        dir = normalize(vec3(-1.0, -v, u));
        break;
    case 2:
        dir = normalize(vec3(u, 1.0, v));
        break;
    case 3:
        dir = normalize(vec3(u, -1.0, -v));
        break;
    case 4:
        dir = normalize(vec3(u, -v, 1.0));
        break;
    case 5:
        dir = normalize(vec3(u, -v, -1.0));
        break;
    }

    return dir;
}

float AreaIntegral(float x, float y)
{
    return atan(x * y, sqrt(x * x + y * y + 1.0));
}

float TexelSolidAngle(vec2 uv)
{
    vec2 texel_size = vec2(1.0) / vec2(cubemap_dimensions.xy);

    float x0 = uv.x - texel_size.x;
    float y0 = uv.y - texel_size.y;
    float x1 = uv.x + texel_size.x;
    float y1 = uv.y + texel_size.y;

    return AreaIntegral(x0, y0) - AreaIntegral(x0, y1) - AreaIntegral(x1, y0) + AreaIntegral(x1, y1);
}

float[9] ProjectOntoSHBands(vec3 dir)
{
    float[9] sh;

    // Band 0
    sh[0] = 0.282095f;

    // Band 1
    sh[1] = 0.488603f * dir.y;
    sh[2] = 0.488603f * dir.z;
    sh[3] = 0.488603f * dir.x;

    // Band 2
    sh[4] = 1.092548f * dir.x * dir.y;
    sh[5] = 1.092548f * dir.y * dir.z;
    sh[6] = 0.315392f * (3.0f * dir.z * dir.z - 1.0f);
    sh[7] = 1.092548f * dir.x * dir.z;
    sh[8] = 0.546274f * (dir.x * dir.x - dir.y * dir.y);

    return sh;
}

float[27] ProjectOntoSH9Color(vec3 dir, vec3 color)
{
    float[9] bands = ProjectOntoSHBands(dir);
    
    float[27] sh_colors;

    for(uint i = 0; i < 9; ++i) {
        sh_colors[i * 3] = color.r * bands[i];
        sh_colors[i * 3 + 1] = color.g * bands[i];
        sh_colors[i * 3 + 2] = color.b * bands[i];
    }

    return sh_colors;
}

vec4 SampleSky(vec3 dir)
{
    if (current_env_probe.texture_index != ~0u) {
        uint probe_texture_index = max(0, min(current_env_probe.texture_index, HYP_MAX_BOUND_REFLECTION_PROBES - 1));

        return Texture2D(sampler_linear, env_probe_textures[probe_texture_index], EncodeOctahedralCoord(normalize(dir)) * 0.5 + 0.5);
    }

    return vec4(0.0);
}

vec4 CalculateDirectLighting(in vec3 P, in vec3 N, in vec3 V)
{
    if (light.type != HYP_LIGHT_TYPE_DIRECTIONAL) {
        return vec4(0.0);
    }

    const vec4 light_color = UINT_TO_VEC4(light.color_encoded);

    vec3 L = normalize(light.position_intensity.xyz);

    float NdotL = max(0.0001, dot(N, L));

    float shadow = 1.0;

    if (light.shadow_map_index != ~0u) {
        shadow = GetShadowStandard(light.shadow_map_index, P, vec2(0.0), NdotL);
    }

    return light_color * NdotL * shadow * light.position_intensity.w;
}

#ifdef MODE_REDUCE
// shared memory (per face) at the final reduce stage
shared vec4 shared_memory[9][6];
#endif

void main(void)
{
#ifdef MODE_CLEAR
    const uvec2 sample_index = uvec2(gl_GlobalInvocationID.xy);

    for (int i = 0; i < 9; i++) {
        CURRENT_TILE.coeffs_weights[i] = vec4(0.0);
    }

    // @TODO Optimize this
    if (all(equal(gl_GlobalInvocationID.xyz, ivec3(0)))) {
        for (int i = 0; i < 9; i++) {
            env_probes[env_probe_index].sh[i] = vec4(0.0);
            // sh_grid_buffer[min(probe_grid_position.w * 9 + i, SH_GRID_BUFFER_SIZE - 1)] = vec4(0.0);
        }
    }
#elif defined(MODE_BUILD_COEFFICIENTS)
    const uvec2 sample_index = uvec2(gl_GlobalInvocationID.xy);

    const vec2 uv = (vec2(sample_index) + 0.5) / vec2(NUM_SAMPLES_X, NUM_SAMPLES_Y);
    const vec2 sample_point = uv * 2.0 - 1.0;
    const vec3 dir = normalize(DecodeOctahedralCoord(uv));

    vec4 sky = SampleSky(dir);

    vec4 albedo = TextureCube(sampler_linear, cubemap_color, dir);
    vec3 normal = normalize(UnpackNormalVec2(TextureCube(sampler_nearest, cubemap_normals, dir).rg));
    vec2 dist_dist2 = TextureCube(sampler_nearest, cubemap_depth, dir).rg;

    vec4 position = vec4(world_position.xyz + dir * dist_dist2.r, 1.0);

    vec4 indirect = SampleSky(normal);
    vec4 irradiance = indirect * albedo * 0.25;

    vec4 radiance = CalculateDirectLighting(position.xyz, normal, -dir) * albedo;
    // radiance = mix(radiance, sky, step(HYP_FLOAT_MAX, dist_dist2.r));

    vec4 color = irradiance + radiance;

    float sh_values[27] = ProjectOntoSH9Color(dir, color.rgb);

    float temp = 1.0 + sample_point.x * sample_point.x + sample_point.y * sample_point.y;
    float weight = 4.0 / (sqrt(temp) * temp);
    
    // float weight = TexelSolidAngle(sample_point);

    for (int i = 0; i < 9; i++) {
        vec3 sh_color = vec3(
            sh_values[i * 3],
            sh_values[i * 3 + 1],
            sh_values[i * 3 + 2]
        );

        CURRENT_TILE.coeffs_weights[i] = vec4(sh_color * weight, weight);
    }
#elif defined(MODE_REDUCE)
    const int face_index = int(gl_GlobalInvocationID.x);

    if (face_index >= 6) {
        return;
    }

    const ivec2 input_index = ivec2(gl_GlobalInvocationID.yz * 2);
    const ivec2 output_index = ivec2(gl_GlobalInvocationID.yz);

    const ivec2 prev_dimensions = ivec2(level_dimensions.xy);
    const ivec2 next_dimensions = ivec2(level_dimensions.zw);

    if (any(greaterThanEqual(input_index, prev_dimensions))) {
        return;
    }

    if (any(greaterThanEqual(input_index + 1, prev_dimensions))) {
        return;
    }

    if (any(greaterThanEqual(output_index, next_dimensions))) {
        return;
    }

    for (int i = 0; i < 9; i++) {
        sh_tiles_output[(face_index * next_dimensions.x * next_dimensions.y) + (output_index.x * next_dimensions.y) + output_index.y].coeffs_weights[i] =
            sh_tiles[(face_index * prev_dimensions.x * prev_dimensions.y) + (input_index.x * prev_dimensions.y) + input_index.y].coeffs_weights[i]
            + sh_tiles[(face_index * prev_dimensions.x * prev_dimensions.y) + ((input_index.x + 1) * prev_dimensions.y) + input_index.y].coeffs_weights[i]
            + sh_tiles[(face_index * prev_dimensions.x * prev_dimensions.y) + ((input_index.x + 1) * prev_dimensions.y) + (input_index.y + 1)].coeffs_weights[i]
            + sh_tiles[(face_index * prev_dimensions.x * prev_dimensions.y) + (input_index.x * prev_dimensions.y) + (input_index.y + 1)].coeffs_weights[i];
    }
#elif defined(MODE_FINALIZE)

#ifdef PARALLEL_REDUCE
    for (int face_index = 0; face_index < 6; face_index++) {
        for (int i = 0; i < 9; i++) {
            env_probes[env_probe_index].sh[i] += sh_tiles[face_index * 9 + i].coeffs_weights[i];
            // sh_grid_buffer[min(probe_grid_position.w * 9 + i, SH_GRID_BUFFER_SIZE - 1)] += sh_tiles[face_index].coeffs_weights[i];
        }
    }

    for (int i = 0; i < 9; i++) {
        float weight = env_probes[env_probe_index].sh[i].a;

        env_probes[env_probe_index].sh[i] *= (4.0 * HYP_FMATH_PI) / max(weight, 0.0001);

        // float weight = sh_grid_buffer[min(probe_grid_position.w * 9 + i, SH_GRID_BUFFER_SIZE - 1)].a;
    
        // sh_grid_buffer[min(probe_grid_position.w * 9 + i, SH_GRID_BUFFER_SIZE - 1)] *= (4.0 * HYP_FMATH_PI) / max(weight, 0.0001);
    }
#else
    float total_weight = 0.0;

    vec3 sh_result[9];

    for (int i = 0; i < 9; i++) {
        sh_result[i] = vec3(0.0);
    }

    for (int sample_x = 0; sample_x < NUM_SAMPLES_X; ++sample_x) {
        for (int sample_y = 0; sample_y < NUM_SAMPLES_Y; ++sample_y) {
            for (int i = 0; i < 9; i++) {
                const vec3 coeff = sh_tiles[(sample_x * NUM_SAMPLES_Y) + sample_y].coeffs_weights[i].rgb;
                const float weight = sh_tiles[(sample_x * NUM_SAMPLES_Y) + sample_y].coeffs_weights[i].a;

                sh_result[i] += coeff;

                total_weight += weight;
            }
        }
    }

    for (int i = 0; i < 9; i++) {
        vec3 result = vec3(sh_result[i] *= (4.0 * HYP_FMATH_PI) / total_weight);

        env_probes[env_probe_index].sh[i] = vec4(result, 1.0);
    }
#endif

#endif
}
