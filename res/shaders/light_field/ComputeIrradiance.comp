
#version 450

#extension GL_GOOGLE_include_directive : require

#define WORKGROUP_SIZE 8

layout(local_size_x = WORKGROUP_SIZE, local_size_y = WORKGROUP_SIZE) in;

#define HYP_DO_NOT_DEFINE_DESCRIPTOR_SETS

#define HYP_SAMPLER_NEAREST sampler_nearest
#define HYP_SAMPLER_LINEAR sampler_linear

#include "../include/defines.inc"
#include "../include/shared.inc"
#include "../include/noise.inc"
#include "../include/packing.inc"
#include "../include/env_probe.inc"
#include "../include/scene.inc"
#include "../include/Octahedron.glsl"
#include "../include/brdf.inc"

HYP_DESCRIPTOR_CBUFF(Global, SphereSamplesBuffer) uniform SphereSamplesBuffer
{
    vec4 sphere_samples[4096];
};

HYP_DESCRIPTOR_SRV(LightFieldProbeDescriptorSet, InColorImage) uniform textureCube color_texture;
HYP_DESCRIPTOR_SRV(LightFieldProbeDescriptorSet, InNormalsImage) uniform textureCube normals_texture;
HYP_DESCRIPTOR_SRV(LightFieldProbeDescriptorSet, InDepthImage) uniform textureCube depth_texture;

HYP_DESCRIPTOR_SAMPLER(LightFieldProbeDescriptorSet, SamplerLinear) uniform sampler sampler_linear;
HYP_DESCRIPTOR_SAMPLER(LightFieldProbeDescriptorSet, SamplerNearest) uniform sampler sampler_nearest;

HYP_DESCRIPTOR_UAV(LightFieldProbeDescriptorSet, OutColorImage, format = rgba8) uniform image2D out_color_image;
HYP_DESCRIPTOR_UAV(LightFieldProbeDescriptorSet, OutDepthImage, format = rg16f) uniform image2D out_depth_image;

HYP_DESCRIPTOR_CBUFF(LightFieldProbeDescriptorSet, UniformBuffer) uniform UniformBuffer
{
    uvec4 image_dimensions;
    uvec4 probe_grid_position;
    uvec4 dimension_per_probe;
    uvec4 probe_offset_coord;

    uint num_bound_lights;
    uint _pad0;
    uint _pad1;
    uint _pad2;

    uvec4 light_indices[4];
};

HYP_DESCRIPTOR_SRV(Global, EnvProbeTextures, count = 16) uniform texture2D env_probe_textures[16];
HYP_DESCRIPTOR_SSBO(Global, EnvProbesBuffer) readonly buffer EnvProbesBuffer
{
    EnvProbe env_probes[];
};
HYP_DESCRIPTOR_SSBO_DYNAMIC(Global, CurrentEnvProbe) readonly buffer CurrentEnvProbe
{
    EnvProbe current_env_probe;
};

HYP_DESCRIPTOR_SSBO(Global, LightsBuffer) readonly buffer LightsBuffer
{
    Light lights[];
};
HYP_DESCRIPTOR_SSBO(Global, ShadowMapsBuffer) readonly buffer ShadowMapsBuffer
{
    ShadowMap shadow_map_data[];
};
HYP_DESCRIPTOR_SRV(Global, ShadowMapsTextureArray) uniform texture2DArray shadow_maps;
HYP_DESCRIPTOR_SRV(Global, PointLightShadowMapsTextureArray) uniform textureCubeArray point_shadow_maps;

#include "../include/shadows.inc"
#include "../include/Octahedron.glsl"

// #define LIGHTING

vec2 NormalizeOctahedralCoord(uvec2 local_coord)
{
    return (vec2(local_coord) + vec2(0.5)) * (vec2(2.0) / vec2(dimension_per_probe)) - vec2(1.0);
}

struct ConvolveResult
{
    vec4 color;
    vec2 dist_dist2;
};

vec4 SampleSky(vec3 dir)
{
    if (current_env_probe.texture_index != ~0u)
    {
        uint probe_texture_index = max(0, min(current_env_probe.texture_index, HYP_MAX_BOUND_REFLECTION_PROBES - 1));

        return Texture2D(sampler_linear, env_probe_textures[probe_texture_index], EncodeOctahedralCoord(normalize(dir)) * 0.5 + 0.5);
    }

    return vec4(0.0);
}

vec4 CalculateDirectLighting(uint light_index, in vec4 albedo, in vec3 P, in vec3 N, in vec3 V)
{
    if (lights[light_index].type != HYP_LIGHT_TYPE_DIRECTIONAL)
    {
        return vec4(0.0);
    }

    const vec4 light_color = vec4(UINT_TO_VEC4(lights[light_index].color_encoded).rgb, 1.0);

    vec3 L = normalize(lights[light_index].position_intensity.xyz);

    float NdotL = max(0.0001, dot(N, L));

    float shadow = 1.0;

    if (lights[light_index].type == HYP_LIGHT_TYPE_DIRECTIONAL && lights[light_index].shadow_map_index != ~0u)
    {
        shadow = GetShadowStandard(lights[light_index].shadow_map_index, P, vec2(0.0), NdotL);
    }

    return vec4((light_color * NdotL * shadow * lights[light_index].position_intensity.w).rgb, 1.0);
}

vec4 ConvoleProbe(uvec2 local_coord)
{
    const int num_samples = 1024;
    const float lobe_size = 0.98;

    const vec2 oct_coord = NormalizeOctahedralCoord(local_coord);
    const vec3 dir = DecodeOctahedralCoord(oct_coord);

    vec4 sum_radiance = vec4(0.0);

    uint seed = gl_GlobalInvocationID.x + gl_GlobalInvocationID.y * 1000 + probe_grid_position.w * 1000000;

    for (int i = 0; i < num_samples; i++)
    {
        vec3 offset = sphere_samples[i % 4096].xyz;
        vec3 sample_dir = normalize(dir + vec3(lobe_size) * offset);

        vec3 normal = normalize(UnpackNormalVec2(TextureCube(sampler_nearest, normals_texture, sample_dir).rg));

        vec2 dist_dist2 = TextureCube(sampler_linear, depth_texture, sample_dir).rg;

        vec4 position = vec4(env_probes[probe_grid_position.w].world_position.xyz + sample_dir * dist_dist2.r, 1.0);

        // Non-accurate lighting calculation for simplicity

        vec4 albedo = TextureCube(sampler_linear, color_texture, sample_dir);

#ifdef LIGHTING
        // Sample the sky in the direction of the normal to get a rough approximation of indirect lighting.
        vec4 irradiance = SampleSky(normal) * albedo * 0.25;

        vec4 radiance = vec4(0.0);

        for (uint j = 0; j < num_bound_lights; j++)
        {
            uint light_index = light_indices[j / 4][j % 4];

            radiance += CalculateDirectLighting(light_index, albedo, position.xyz, normal, -sample_dir);
        }

        vec4 color = irradiance + radiance * albedo; // mix(irradiance + radiance * albedo, SampleSky(sample_dir), step(HYP_FLOAT_MAX, dist_dist2.r));
#else
        vec4 color = albedo;
#endif

        sum_radiance += color;
    }

    return sum_radiance / float(num_samples);
}

void main(void)
{
    if (any(greaterThanEqual(gl_GlobalInvocationID.xy, dimension_per_probe.xy)))
    {
        return;
    }

    const uvec2 local_coord = gl_GlobalInvocationID.xy;

    vec4 color = ConvoleProbe(local_coord);

    const uvec2 coord = probe_offset_coord.xy + local_coord;

    imageStore(out_color_image, ivec2(coord), color);
}