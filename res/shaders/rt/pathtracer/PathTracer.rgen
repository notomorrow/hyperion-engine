#version 460
#extension GL_GOOGLE_include_directive : require
#extension GL_EXT_ray_tracing : require
#extension GL_EXT_scalar_block_layout : require

#include "../../include/defines.inc"
#include "../../include/noise.inc"
#include "../../include/packing.inc"
#include "../../include/tonemap.inc"

#ifdef HYP_FEATURES_DYNAMIC_DESCRIPTOR_INDEXING
HYP_DESCRIPTOR_SRV(Scene, GBufferTextures, count = 8) uniform texture2D gbuffer_textures[8];
#else
HYP_DESCRIPTOR_SRV(Scene, GBufferAlbedoTexture) uniform texture2D gbuffer_albedo_texture;
HYP_DESCRIPTOR_SRV(Scene, GBufferNormalsTexture) uniform texture2D gbuffer_normals_texture;
HYP_DESCRIPTOR_SRV(Scene, GBufferMaterialTexture) uniform texture2D gbuffer_material_texture;
HYP_DESCRIPTOR_SRV(Scene, GBufferVelocityTexture) uniform texture2D gbuffer_velocity_texture;
HYP_DESCRIPTOR_SRV(Scene, GBufferLightmapTexture) uniform texture2D gbuffer_albedo_lightmap_texture;
HYP_DESCRIPTOR_SRV(Scene, GBufferMaskTexture) uniform texture2D gbuffer_mask_texture;
HYP_DESCRIPTOR_SRV(Scene, GBufferWSNormalsTexture) uniform texture2D gbuffer_ws_normals_texture;
HYP_DESCRIPTOR_SRV(Scene, GBufferTranslucentTexture) uniform texture2D gbuffer_albedo_texture_translucent;
#endif

HYP_DESCRIPTOR_SRV(Scene, GBufferMipChain) uniform texture2D gbuffer_mip_chain;
HYP_DESCRIPTOR_SRV(Scene, GBufferDepthTexture) uniform texture2D gbuffer_depth_texture;

HYP_DESCRIPTOR_SAMPLER(Global, SamplerNearest) uniform sampler sampler_nearest;
HYP_DESCRIPTOR_SAMPLER(Global, SamplerLinear) uniform sampler sampler_linear;

#define texture_sampler sampler_linear
#define HYP_SAMPLER_NEAREST sampler_nearest
#define HYP_SAMPLER_LINEAR sampler_linear

HYP_DESCRIPTOR_ACCELERATION_STRUCTURE(RTRadianceDescriptorSet, TLAS) uniform accelerationStructureEXT tlas;
HYP_DESCRIPTOR_UAV(RTRadianceDescriptorSet, OutputImage, format = rgba8) uniform image2D image;

#define HYP_DO_NOT_DEFINE_DESCRIPTOR_SETS

#include "../../include/gbuffer.inc"
#include "../../include/scene.inc"
#include "../../include/shared.inc"
#include "../../include/packing.inc"
#include "../../include/noise.inc"
#include "../../include/brdf.inc"
#include "../../include/env_probe.inc"

/// Blue noise
HYP_DESCRIPTOR_SSBO(Global, BlueNoiseBuffer) readonly buffer BlueNoiseBuffer
{
	ivec4 sobol_256spp_256d[256 * 256 / 4];
	ivec4 scrambling_tile[128 * 128 * 8 / 4];
	ivec4 ranking_tile[128 * 128 * 8 / 4];
};

#include "../../include/BlueNoise.glsl"

#undef HYP_DO_NOT_DEFINE_DESCRIPTOR_SETS

HYP_DESCRIPTOR_SSBO(RTRadianceDescriptorSet, LightsBuffer) readonly buffer LightsBuffer
{
    Light lights[];
};

HYP_DESCRIPTOR_CBUFF_DYNAMIC(Global, WorldsBuffer) uniform WorldsBuffer
{
    WorldShaderData world_shader_data;
};

HYP_DESCRIPTOR_CBUFF_DYNAMIC(Global, CamerasBuffer) uniform CameraShaderData
{
    Camera camera;
};

HYP_DESCRIPTOR_SRV(Global, EnvProbeTextures, count = 16) uniform texture2D env_probe_textures[16];
HYP_DESCRIPTOR_SSBO(Global, EnvProbesBuffer) readonly buffer EnvProbesBuffer { EnvProbe env_probes[]; };
HYP_DESCRIPTOR_CBUFF_DYNAMIC(Global, EnvGridsBuffer) uniform EnvGridsBuffer { EnvGrid env_grid; };
HYP_DESCRIPTOR_SSBO_DYNAMIC(Global, CurrentEnvProbe) readonly buffer CurrentEnvProbe { EnvProbe current_env_probe; };

#include "../../include/Octahedron.glsl"

#define PATHTRACER
#include "../../include/rt/RTRadiance.inc"
#include "../../include/rt/payload.inc"

HYP_DESCRIPTOR_CBUFF(RTRadianceDescriptorSet, RTRadianceUniforms) uniform RTRadianceUniformBuffer
{
    RTRadianceUniforms rt_radiance_uniforms;
};

layout(location = 0) rayPayloadEXT RayPayload payload;

#define RAY_OFFSET 0.25
#define NUM_SAMPLES 4
#define NUM_BOUNCES 4

struct RayBounceInfo
{
    vec4 throughput;
    vec4 emissive;
    vec4 radiance; // set in 2nd pass
};

void main() 
{
    const ivec2 resolution = rt_radiance_uniforms.output_image_resolution;
    
    const int pixel_index = int(gl_LaunchIDEXT.x * 2) + int(world_shader_data.frame_counter & 1);
    const ivec2 storage_coord = ivec2(
        pixel_index % resolution.x,
        pixel_index / resolution.x
    );

    //if (pixel_index % 2 == 0) {
    //    return;
    //}

    const vec2 uv = (vec2(storage_coord) + 0.5) / vec2(resolution);

    const mat4 view_inverse = inverse(camera.view);
    const mat4 projection_inverse = inverse(camera.projection);

    const vec3 normal = normalize(DecodeNormal(Texture2DLod(sampler_nearest, gbuffer_normals_texture, uv, 0.0)));
    const float depth = Texture2DLod(sampler_nearest, gbuffer_depth_texture, uv, 0.0).r;
    const vec3 position = ReconstructWorldSpacePositionFromDepth(projection_inverse, view_inverse, uv, depth).xyz;
    const vec4 material_data = Texture2DLod(sampler_nearest, gbuffer_material_texture, uv, 0.0);
    const float roughness = material_data.r;
    const float perceptual_roughness = sqrt(roughness);
    const float metalness = material_data.g;

    const vec3 V = normalize(camera.position.xyz - position);
    const vec3 R = normalize(reflect(-V, normal));

    const uint flags = gl_RayFlagsOpaqueEXT;
    const uint mask = 0xff;
    const float tmin = 0.1;
    const float tmax = 1000.0;

    uint ray_seed = InitRandomSeed(InitRandomSeed(gl_LaunchIDEXT.x * 2, gl_LaunchIDEXT.x * 2 + 1), world_shader_data.frame_counter % 256);

    vec4 color = vec4(0.0);

    const vec4 target = projection_inverse * vec4(uv * 2.0 - 1.0, 1.0, 1.0);
    const vec4 albedo = Texture2DLod(sampler_nearest, gbuffer_albedo_texture, uv, 0.0);

    vec4 irradiance = vec4(0.0, 0.0, 0.0, 0.0);
    vec4 radiance = vec4(0.0, 0.0, 0.0, 0.0);

    for (uint sample_index = 0; sample_index < NUM_SAMPLES; sample_index++) {
        vec2 rnd = vec2(RandomFloat(ray_seed), RandomFloat(ray_seed));

        vec3 tangent;
        vec3 bitangent;
        ComputeOrthonormalBasis(normal, tangent, bitangent);

        vec3 H = ImportanceSampleGGX(rnd, normal, roughness);
        H = tangent * H.x + bitangent * H.y + normal * H.z;
        H = normalize(H);

        vec3 origin = position + normal * RAY_OFFSET;

#if 0
        { // radiance
            vec4 emissive = vec4(0.0);

            for (uint light_index = 0; light_index < rt_radiance_uniforms.num_bound_lights; light_index++) {
                const Light light = HYP_GET_LIGHT(light_index);

                if (light.type != HYP_LIGHT_TYPE_DIRECTIONAL) {
                    continue;
                }

                payload.distance = -1.0;

                traceRayEXT(tlas, flags, mask, 0, 0, 0, origin, tmin, normalize(light.position_intensity.xyz), tmax, 0);

                if (payload.distance < 0.0) {
                    const float cos_theta = max(dot(normal, normalize(light.position_intensity.xyz)), 0.0);
                    emissive += UINT_TO_VEC4(light.color_encoded) * light.position_intensity.w * cos_theta;
                }
            }

            // @TODO: Proper BRDF
            radiance += emissive * (albedo * (1.0 - metalness));
        }
#endif

        { // irradiance
            RayBounceInfo bounces[NUM_BOUNCES];

            bounces[0].throughput = (albedo * (1.0 - metalness));
            bounces[0].emissive = vec4(0.0);
            bounces[0].radiance = vec4(0.0);

            uint num_bounces = 1;
            
            vec3 direction = reflect(-V, H);

            payload.throughput = vec4(1.0);
            payload.emissive = vec4(0.0);
            payload.normal = normal;

            for (int bounce_index = 1; bounce_index < NUM_BOUNCES; bounce_index++) {
                payload.distance = -1.0;

                vec4 throughput = payload.throughput;
                vec3 normal = payload.normal;
                traceRayEXT(tlas, flags, mask, 0, 0, 0, origin, tmin, direction, tmax, 0);

                bounces[bounce_index].throughput = payload.throughput;
                bounces[bounce_index].emissive = payload.emissive;
                bounces[bounce_index].radiance = vec4(0.0);

                if (payload.distance < 0.0) {
                    // hit the sky, sample the environment map
                    if (current_env_probe.texture_index != ~0u) {
                       uint probe_texture_index = max(0, min(current_env_probe.texture_index, HYP_MAX_BOUND_REFLECTION_PROBES - 1));

                       bounces[bounce_index].emissive += Texture2D(sampler_linear, env_probe_textures[probe_texture_index], EncodeOctahedralCoord(direction) * 0.5 + 0.5);
                    }

                    for (uint light_index = 0; light_index < rt_radiance_uniforms.num_bound_lights; light_index++) {
                        const Light light = HYP_GET_LIGHT(light_index);

                        if (light.type != HYP_LIGHT_TYPE_DIRECTIONAL) {
                            continue;
                        }

                        const float cos_theta = max(dot(direction, normalize(light.position_intensity.xyz)), 0.0);

                        bounces[bounce_index].emissive += UINT_TO_VEC4(light.color_encoded) * light.position_intensity.w * cos_theta;
                    }

                    ++num_bounces;

                    break;
                }
            
                vec3 hit_position = origin + direction * payload.distance;

                direction = normalize(RandomInHemisphere(
                    vec3(RandomFloat(ray_seed), RandomFloat(ray_seed), RandomFloat(ray_seed)),
                    payload.normal
                ));

                origin = hit_position + direction * RAY_OFFSET;

                ++num_bounces;
            }

            int bounce_index = int(num_bounces) - 1;

            for (; bounce_index >= 0; bounce_index--) {
                vec4 r = bounces[bounce_index].emissive;

                if (bounce_index != num_bounces - 1) {
                    r += bounces[bounce_index + 1].radiance * bounces[bounce_index].throughput;
                }

                // As the throughput is multiplied by the albedo, the probability of the ray getting discarded is higher
                float p = max(r.r, max(r.g, max(r.b, r.a)));

                if (RandomFloat(ray_seed) > p) {
                    break;
                }

                r /= max(p, 0.0001);
                
                bounces[bounce_index].radiance = r;
            }
            
            // If ray has not been discarded then bounces[0] will have final radiance set,
            // otherwise it will be zeros
            irradiance += bounces[0].radiance;
        }
    }

    color = irradiance / float(NUM_SAMPLES);
    //color += radiance / float(NUM_SAMPLES);

    // color.rgb = pow(color.rgb, vec3(1.0 / 2.2));
    
    // float max_component = max(color.r, max(color.g, color.b));

    // if (max_component < 0.0001) {
    //     // Discard the pixel if the color is too dark so it can accumulate more samples
    //     return;
    // }

    imageStore(image, storage_coord, color);
}
