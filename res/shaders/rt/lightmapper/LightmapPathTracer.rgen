#version 460
#extension GL_GOOGLE_include_directive : require
#extension GL_EXT_ray_tracing : require
#extension GL_EXT_scalar_block_layout : require

#include "../../include/defines.inc"
#include "../../include/noise.inc"
#include "../../include/packing.inc"
#include "../../include/tonemap.inc"

HYP_DESCRIPTOR_SAMPLER(Global, SamplerNearest) uniform sampler sampler_nearest;
HYP_DESCRIPTOR_SAMPLER(Global, SamplerLinear) uniform sampler sampler_linear;

#define texture_sampler sampler_linear
#define HYP_SAMPLER_NEAREST sampler_nearest
#define HYP_SAMPLER_LINEAR sampler_linear

HYP_DESCRIPTOR_ACCELERATION_STRUCTURE(RTRadianceDescriptorSet, TLAS) uniform accelerationStructureEXT tlas;

#define HYP_DO_NOT_DEFINE_DESCRIPTOR_SETS

#include "../../include/scene.inc"
#include "../../include/shared.inc"
#include "../../include/packing.inc"
#include "../../include/noise.inc"
#include "../../include/brdf.inc"
#include "../../include/env_probe.inc"

/// Blue noise
HYP_DESCRIPTOR_SSBO(Global, BlueNoiseBuffer, size = 1310720) readonly buffer BlueNoiseBuffer
{
	ivec4 sobol_256spp_256d[256 * 256 / 4];
	ivec4 scrambling_tile[128 * 128 * 8 / 4];
	ivec4 ranking_tile[128 * 128 * 8 / 4];
};

#include "../../include/BlueNoise.glsl"

#undef HYP_DO_NOT_DEFINE_DESCRIPTOR_SETS

HYP_DESCRIPTOR_SSBO(RTRadianceDescriptorSet, LightsBuffer) readonly buffer LightsBuffer
{
    Light lights[];
};

HYP_DESCRIPTOR_SSBO_DYNAMIC(Scene, ScenesBuffer) readonly buffer SceneBuffer
{
    Scene scene;
};

HYP_DESCRIPTOR_SRV(Scene, EnvProbeTextures, count = 16) uniform textureCube env_probe_textures[16];
HYP_DESCRIPTOR_SSBO(Scene, EnvProbesBuffer) readonly buffer EnvProbesBuffer { EnvProbe env_probes[]; };
HYP_DESCRIPTOR_CBUFF_DYNAMIC(Scene, EnvGridsBuffer) uniform EnvGridsBuffer { EnvGrid env_grid; };
HYP_DESCRIPTOR_SSBO(Scene, SHGridBuffer) readonly buffer SHGridBuffer { vec4 sh_grid_buffer[SH_GRID_BUFFER_SIZE]; };
HYP_DESCRIPTOR_SSBO_DYNAMIC(Scene, CurrentEnvProbe) readonly buffer CurrentEnvProbe { EnvProbe current_env_probe; };

#define PATHTRACER
#define LIGHTMAPPER
#include "../../include/rt/RTRadiance.inc"
#include "../../include/rt/payload.inc"

layout(location = 0) rayPayloadEXT RayPayload payload;

HYP_DESCRIPTOR_SSBO(RTRadianceDescriptorSet, HitsBuffer, standard = std140) buffer HitsBuffer
{
    vec4    hits[512 * 512];
};

struct LightmapRay
{
    vec4    origin;
    vec4    direction;
};

HYP_DESCRIPTOR_SSBO(RTRadianceDescriptorSet, RaysBuffer, standard = std140) readonly buffer RaysBuffer
{
    LightmapRay ray_data[512 * 512];
};

HYP_DESCRIPTOR_CBUFF(RTRadianceDescriptorSet, RTRadianceUniforms) uniform RTRadianceUniformBuffer
{
    RTRadianceUniforms  rt_radiance_uniforms;
};

#ifdef MODE_IRRADIANCE
#define RAY_OFFSET 0.005
#define NUM_BOUNCES 4

#define NUM_SAMPLES 4
#else
#define RAY_OFFSET 0.0005
#define NUM_BOUNCES 1
#define NUM_SAMPLES 1
#endif

// uncomment to see the actual object colors
//#define DEBUG_TEST

struct RayBounceInfo
{
    vec4 throughput;
    vec4 emissive;
    vec4 radiance; // set in 2nd pass
};

void main() 
{
    const uint ray_index = gl_LaunchIDEXT.x;

    const LightmapRay ray = ray_data[ray_index];

    const uint flags = gl_RayFlagsOpaqueEXT;
    const uint mask = 0xff;
    const float tmin = 0.1;
    const float tmax = 1000.0;

    const vec3 normal = ray.direction.xyz;

    vec3 tangent;
    vec3 bitangent;

    vec4 radiance = vec4(0.0);

    uint ray_seed = InitRandomSeed(((rt_radiance_uniforms.ray_offset + ray_index) * 2), ((rt_radiance_uniforms.ray_offset + ray_index) * 2) + 1);

    for (uint sample_index = 0; sample_index < NUM_SAMPLES; sample_index++) {
        ComputeOrthonormalBasis(normal, tangent, bitangent);

        vec2 rnd = vec2(RandomFloat(ray_seed), RandomFloat(ray_seed));
        vec3 H;

        vec3 direction;

#ifdef MODE_IRRADIANCE     
        direction = normalize(RandomInHemisphere(
            vec3(RandomFloat(ray_seed), RandomFloat(ray_seed), RandomFloat(ray_seed)),
            normal
        ));
#else
        direction = normal;
#endif

        vec3 origin = ray.origin.xyz + direction.xyz * RAY_OFFSET;

        payload.throughput = vec4(1.0); // multiplied be albedo when sampled after bake
        payload.emissive = vec4(0.0);
        payload.triangle_index = ~0u;
        payload.normal = normal;

#ifdef MODE_IRRADIANCE

        RayBounceInfo bounces[NUM_BOUNCES];
        uint num_bounces = 0;

        for (int bounce_index = 0; bounce_index < NUM_BOUNCES; bounce_index++) {
            payload.distance = -1.0;

            vec4 throughput = payload.throughput;
            vec3 normal = payload.normal;
            traceRayEXT(tlas, flags, mask, 0, 0, 0, origin, tmin, direction, tmax, 0);

            bounces[bounce_index].throughput = payload.throughput;
            bounces[bounce_index].emissive = payload.emissive;
            bounces[bounce_index].radiance = vec4(0.0);

            if (payload.distance < 0.0) {
                // hit the sky, sample the environment map
                if (current_env_probe.texture_index != ~0u) {
                    uint probe_texture_index = max(0, min(current_env_probe.texture_index, HYP_MAX_BOUND_REFLECTION_PROBES - 1));

                    bounces[bounce_index].emissive += TextureCube(sampler_linear, env_probe_textures[probe_texture_index], direction);
                }
                
                for (uint light_index = 0; light_index < rt_radiance_uniforms.num_bound_lights; light_index++) {
                    const Light light = HYP_GET_LIGHT(light_index);

                    // Only handle directional lights here
                    if (light.type != HYP_LIGHT_TYPE_DIRECTIONAL) {
                        continue;
                    }

                    const float cos_theta = max(dot(direction, normalize(light.position_intensity.xyz)), 0.0);
                    bounces[bounce_index].emissive += UINT_TO_VEC4(light.color_encoded) * vec4(1.0) * light.position_intensity.w * cos_theta;
                }

                ++num_bounces;

                break;
            }
            
            vec3 hit_position = origin + direction * payload.distance;

            direction = normalize(RandomInHemisphere(
                vec3(RandomFloat(ray_seed), RandomFloat(ray_seed), RandomFloat(ray_seed)),
                payload.normal
            ));
            origin = hit_position + direction * RAY_OFFSET;

            /*ComputeOrthonormalBasis(payload.normal, tangent, bitangent);

            H = ImportanceSampleGGX(rnd, payload.normal, payload.roughness);
            H = tangent * H.x + bitangent * H.y + payload.normal * H.z;
            H = normalize(H);
                
            direction = reflect(direction, H);*/

            ++num_bounces;
        }

        for (int bounce_index = int(num_bounces - 1); bounce_index >= 0; bounce_index--) {
            vec4 radiance = bounces[bounce_index].emissive;

            if (bounce_index != num_bounces - 1) {
                radiance += bounces[bounce_index + 1].radiance * bounces[bounce_index].throughput;
            }

            // As the throughput is multiplied by the albedo, the probability of the ray getting discarded is higher
            float p = max(radiance.r, max(radiance.g, max(radiance.b, radiance.a)));

            if (RandomFloat(ray_seed) > p) {
                break;
            }

            radiance /= max(p, 0.0001);
            
            bounces[bounce_index].radiance = radiance;
        }

#ifdef DEBUG_TEST
        radiance = num_bounces > 0 ? bounces[0].throughput : vec4(0.0);
#else
        radiance += num_bounces > 0 ? bounces[0].radiance : vec4(0.0);
#endif

#elif defined(MODE_RADIANCE)
        // test directional lights
        vec4 emissive = vec4(0.0);

        for (uint light_index = 0; light_index < rt_radiance_uniforms.num_bound_lights; light_index++) {
            const Light light = HYP_GET_LIGHT(light_index);

            if (light.type != HYP_LIGHT_TYPE_DIRECTIONAL) {
                continue;
            }

            payload.distance = -1.0;

            const vec3 L = normalize(light.position_intensity.xyz);

            traceRayEXT(tlas, flags, mask, 0, 0, 0, origin, tmin, L, tmax, 0);

            if (payload.distance < 0.0) {
                const float cos_theta = max(dot(normal, L), 0.0);
                emissive += /*UINT_TO_VEC4(light.color_encoded) **/ vec4(1.0) * light.position_intensity.w * cos_theta;
            }
        }

        radiance += emissive;
#endif
    }

    vec4 color = radiance / float(NUM_SAMPLES);

    hits[ray_index] = color;
}
