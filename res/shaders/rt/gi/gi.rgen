#version 460
#extension GL_GOOGLE_include_directive : require
#extension GL_EXT_ray_tracing : enable
#extension GL_EXT_scalar_block_layout : enable

#include "../../include/defines.inc"
#include "../../include/noise.inc"
#include "../../include/packing.inc"

HYP_DESCRIPTOR_SAMPLER(Global, SamplerNearest) uniform sampler sampler_nearest;
HYP_DESCRIPTOR_SAMPLER(Global, SamplerLinear) uniform sampler sampler_linear;

#define texture_sampler sampler_linear
#define HYP_SAMPLER_NEAREST sampler_nearest
#define HYP_SAMPLER_LINEAR sampler_linear

#define HYP_DO_NOT_DEFINE_DESCRIPTOR_SETS

#include "../../include/scene.inc"
#include "../../include/shared.inc"
#include "../../include/noise.inc"
#include "../../include/env_probe.inc"

#undef HYP_DO_NOT_DEFINE_DESCRIPTOR_SETS

#include "../../include/rt/payload.inc"
#include "../../include/rt/probe/probe_uniforms.inc"

HYP_DESCRIPTOR_ACCELERATION_STRUCTURE(DDGIDescriptorSet, TLAS) uniform accelerationStructureEXT tlas;

HYP_DESCRIPTOR_CBUFF(DDGIDescriptorSet, DDGIUniforms) uniform DDGIUniformBuffer
{
    DDGIUniforms probe_system;
};

HYP_DESCRIPTOR_SSBO(DDGIDescriptorSet, ProbeRayData) buffer ProbeRayDataBuffer
{
    ProbeRayData probe_rays[];
};

HYP_DESCRIPTOR_SSBO(DDGIDescriptorSet, LightsBuffer) readonly buffer LightsBuffer
{
    Light lights[];
};

#include "../../include/rt/probe/shared.inc"

HYP_DESCRIPTOR_SSBO_DYNAMIC(Scene, ScenesBuffer) readonly buffer SceneBuffer
{
    Scene scene;
};

HYP_DESCRIPTOR_SRV(Scene, EnvProbeTextures, count = 16) uniform textureCube env_probe_textures[16];
HYP_DESCRIPTOR_SSBO(Scene, EnvProbesBuffer) readonly buffer EnvProbesBuffer { EnvProbe env_probes[HYP_MAX_ENV_PROBES]; };
HYP_DESCRIPTOR_CBUFF_DYNAMIC(Scene, EnvGridsBuffer) uniform EnvGridsBuffer { EnvGrid env_grid; };
HYP_DESCRIPTOR_SSBO(Scene, SHGridBuffer) readonly buffer SHGridBuffer { vec4 sh_grid_buffer[SH_GRID_BUFFER_SIZE]; };
HYP_DESCRIPTOR_SSBO_DYNAMIC(Scene, CurrentEnvProbe) readonly buffer CurrentEnvProbe { EnvProbe current_env_probe; };

layout(location = 0) rayPayloadEXT RayPayload payload;

#define HYP_GET_LIGHT(index) \
    lights[probe_system.light_indices[(index / 4)][index % 4]]

#define RAY_OFFSET 1.0
#define NUM_SAMPLES 1

layout(push_constant) uniform constants
{
    mat4 rotation;
    uint time;
} push_constants;

void SetProbeRayData(uvec2 coord, ProbeRayData ray_data)
{
    probe_rays[PROBE_RAY_DATA_INDEX(coord)] = ray_data;
}

void main()
{
    const uvec2 coord = uvec2(gl_LaunchIDEXT.xy);
    
    const uint probe_index = coord.x;
    const uint ray_index = coord.y;
    
    vec3 origin = ProbeIndexToWorldPosition(probe_index);
    vec3 direction = normalize(mat3(push_constants.rotation) * SphericalFibonacci(ray_index, probe_system.num_rays_per_probe));
    
    uint flags = gl_RayFlagsOpaqueEXT;
    uint mask = 0xff;
    float tmin = 0.2;
    float tmax = 10000.0;
    
    payload.color = vec4(0.0);
    payload.distance = -1.0;
    
    traceRayEXT(tlas, flags, mask, 0, 0, 0, origin, tmin, direction, tmax, 0);

    ProbeRayData ray_data;
    ray_data.color = payload.color;
    ray_data.origin = vec4(origin, 1.0);
    ray_data.normal = vec4(payload.normal, 0.0);
    ray_data.direction_depth = vec4(direction, payload.distance);

    if (payload.distance < 0.0) {
        // hit the sky, sample the environment map if available
        if (current_env_probe.texture_index != ~0u) {
            uint probe_texture_index = max(0, min(current_env_probe.texture_index, HYP_MAX_BOUND_REFLECTION_PROBES - 1));

            ray_data.color = TextureCube(sampler_linear, env_probe_textures[probe_texture_index], direction);
        }

        for (uint light_index = 0; light_index < probe_system.num_bound_lights; light_index++) {
            const Light light = HYP_GET_LIGHT(light_index);

            if (light.type != HYP_LIGHT_TYPE_DIRECTIONAL) {
                continue;
            }

            const vec3 L = normalize(light.position_intensity.xyz);

            const float cos_theta = max(dot(direction, L), 0.0);
            ray_data.color += UINT_TO_VEC4(light.color_encoded) * light.position_intensity.w * cos_theta;
        }
    }

    SetProbeRayData(coord, ray_data);
}
