#version 450

#extension GL_GOOGLE_include_directive : require
#extension GL_EXT_scalar_block_layout  : require
#extension GL_EXT_nonuniform_qualifier : require

// #define PASSTHROUGH

layout(local_size_x = 8, local_size_y = 8, local_size_z = 1) in;

#ifndef OUTPUT_FORMAT
    #if defined(OUTPUT_RGBA8)
        #define OUTPUT_FORMAT rgba8
        #define TEMPORAL_BLENDING_GAMMA_CORRECTION
    #elif defined(OUTPUT_RGBA16F)
        #define OUTPUT_FORMAT rgba16f
    #elif defined(OUTPUT_RGBA32F)
        #define OUTPUT_FORMAT rgba32f
    #else
        #define OUTPUT_FORMAT rgba8
        #define TEMPORAL_BLENDING_GAMMA_CORRECTION
    #endif
#endif

HYP_DESCRIPTOR_SRV(TemporalBlendingDescriptorSet, InImage) uniform texture2D input_texture;
HYP_DESCRIPTOR_SRV(TemporalBlendingDescriptorSet, PrevImage) uniform texture2D prev_input_texture;
HYP_DESCRIPTOR_SRV(TemporalBlendingDescriptorSet, VelocityImage) uniform texture2D velocity_texture;
HYP_DESCRIPTOR_SAMPLER(TemporalBlendingDescriptorSet, SamplerLinear) uniform sampler sampler_linear;
HYP_DESCRIPTOR_SAMPLER(TemporalBlendingDescriptorSet, SamplerNearest) uniform sampler sampler_nearest;

// @TODO: Make ShaderCompiler use c preprocessor so that OUTPUT_FORMAT gets resolved
HYP_DESCRIPTOR_UAV(TemporalBlendingDescriptorSet, OutImage, format = rgba16f) uniform writeonly image2D output_image;

#ifdef TEMPORAL_BLEND_TECHNIQUE_0
    #define TEMPORAL_BLEND_TECHNIQUE 0
#elif defined(TEMPORAL_BLEND_TECHNIQUE_1)
    #define TEMPORAL_BLEND_TECHNIQUE 1
#elif defined(TEMPORAL_BLEND_TECHNIQUE_2)
    #define TEMPORAL_BLEND_TECHNIQUE 2
#elif defined(TEMPORAL_BLEND_TECHNIQUE_3)
    #define TEMPORAL_BLEND_TECHNIQUE 3
#elif defined(TEMPORAL_BLEND_TECHNIQUE_4)
    #define TEMPORAL_BLEND_TECHNIQUE 4 // progressive
#else
    #define TEMPORAL_BLEND_TECHNIQUE 3
#endif

#define HYP_DO_NOT_DEFINE_DESCRIPTOR_SETS
#include "./include/defines.inc"
#include "./include/packing.inc"
#include "./include/defines.inc"
#include "./include/packing.inc"
#include "./include/shared.inc"
#include "./include/scene.inc"

#define TEMPORAL_BLENDING_USE_YCoCg

#if TEMPORAL_BLEND_TECHNIQUE == 0
    #undef ADJUST_COLOR_HDR
#elif TEMPORAL_BLEND_TECHNIQUE == 1
    #define ADJUST_COLOR_HDR
#endif

#include "./include/Temporal.glsl"
#undef HYP_DO_NOT_DEFINE_DESCRIPTOR_SETS

HYP_DESCRIPTOR_SSBO_DYNAMIC(Scene, ScenesBuffer, size = 256) readonly buffer SceneBuffer
{
    Scene scene;
};

HYP_DESCRIPTOR_CBUFF_DYNAMIC(Scene, CamerasBuffer, size = 512) uniform CameraShaderData
{
    Camera camera;
};

HYP_DESCRIPTOR_SRV(Global, GBufferDepthTexture) uniform texture2D depth_texture;

layout(push_constant) uniform PushConstant {
    uvec2   output_dimensions;
    uvec2   depth_texture_dimensions;
    uint    blending_frame_counter;
};

void main(void)
{
    const uvec2 coord = gl_GlobalInvocationID.xy;

    if (any(greaterThanEqual(coord, output_dimensions))) {
        return;
    }

    const vec2 uv = (vec2(coord) + 0.5) / vec2(output_dimensions);
    const vec2 texel_size = vec2(1.0) / vec2(output_dimensions);

    vec4 color = Texture2D(sampler_nearest, input_texture, uv);

    vec2 velocity;
    float view_space_depth;

    InitTemporalParams(
        depth_texture,
        velocity_texture,
        depth_texture_dimensions,
        uv,
        camera.near,
        camera.far,
        velocity,
        view_space_depth
    );

#ifdef PASSTHROUGH
    imageStore(output_image, clamp(ivec2(coord), ivec2(0), ivec2(output_dimensions) - 1), color);
#else
#if TEMPORAL_BLEND_TECHNIQUE == 0
    vec4 previous_color = Texture2D(sampler_linear, prev_input_texture, uv - velocity);

    color.a = 1.0;
    previous_color.a = 1.0;

    color = ADJUST_COLOR_IN(color);
    previous_color = ADJUST_COLOR_IN(previous_color);

    vec4 color_ycocg = RGBToYCoCg(color);
    vec4 previous_color_ycocg = RGBToYCoCg(previous_color);

    const float _SubpixelThreshold = 0.5;
    const float _GatherBase = 0.5;
    const float _GatherSubpixelMotion = 0.1666;

    const vec2 texel_vel = velocity / max(vec2(HYP_FMATH_EPSILON), texel_size);
    const float texel_vel_mag = length(texel_vel) * view_space_depth;
    const float subpixel_motion = Saturate(_SubpixelThreshold / max(HYP_FMATH_EPSILON, texel_vel_mag));
    const float min_max_support = _GatherBase + _GatherSubpixelMotion * subpixel_motion;

#ifdef TEMPORAL_BLENDING_USE_YCoCg
    vec4 mean = color_ycocg;
#else
    vec4 mean = color;
#endif

    vec4 stddev = mean * mean;

    const vec2 ss_offset01 = min_max_support * vec2(-texel_size.x, texel_size.y);
    const vec2 ss_offset11 = min_max_support * vec2(texel_size.x, texel_size.y);

    const vec2 offsets[4] = vec2[](
        -ss_offset11,// vec2(-1.0, 0.0),
        -ss_offset01,// vec2(1.0, 0.0),
        ss_offset01,// vec2(0.0, -1.0),
        ss_offset11// vec2(0.0, 1.0)
    );

    for (int i = 0; i < 4; i++) {
        vec4 c = ADJUST_COLOR_IN(Texture2D(sampler_linear, input_texture, uv + offsets[i]));

#ifdef TEMPORAL_BLENDING_USE_YCoCg
        c = RGBToYCoCg(c);
#endif

        mean += c;
        stddev += c * c;
    }

    mean /= 5.0;
    stddev = sqrt(min(stddev / 5.0 - (mean * mean), 0.0));

#ifdef TEMPORAL_BLENDING_USE_YCoCg
    vec4 clipped = ClipToAABB(color_ycocg, previous_color_ycocg, mean, stddev);
#else
    vec4 clipped = ClipToAABB(color, previous_color, mean, stddev);
#endif

    const float lum0 = color_ycocg.r;

#ifdef TEMPORAL_BLENDING_USE_YCoCg
    const float lum1 = clipped.r;
#else
    const float lum1 = RGBToYCoCg(clipped).r;
#endif

    float unbiased_diff = abs(lum0 - lum1) / max(lum0, max(lum1, 0.2));
    float unbiased_weight = 1.0 - unbiased_diff;
    float unbiased_weight_sqr = HYP_FMATH_SQR(unbiased_weight);
    float feedback = saturate(mix(0.96, 0.995, unbiased_weight_sqr));

#ifdef TEMPORAL_BLENDING_USE_YCoCg
    clipped = YCoCgToRGB(clipped);
#endif

    color = ADJUST_COLOR_OUT(mix(color, clipped, feedback));

    color = any(isnan(color)) ? vec4(0.0, 0.0, 1.0, 1.0) : color;

#elif TEMPORAL_BLEND_TECHNIQUE == 1
    color = TemporalResolve(input_texture, prev_input_texture, uv, velocity, texel_size, view_space_depth);

#elif TEMPORAL_BLEND_TECHNIQUE == 2
    color = TemporalBlendRounded(input_texture, prev_input_texture, uv, velocity, texel_size, view_space_depth);
#elif TEMPORAL_BLEND_TECHNIQUE == 3
    color = TemporalBlendVarying(input_texture, prev_input_texture, uv, velocity, texel_size, view_space_depth);
#elif TEMPORAL_BLEND_TECHNIQUE == 4
    vec4 previous_color = Texture2D(sampler_linear, prev_input_texture, uv);

    color.a = 1.0;
    previous_color.a = 1.0;

    color = ADJUST_COLOR_IN(color);
    previous_color = ADJUST_COLOR_IN(previous_color);

    // Moving average over number of frames
    const float blending = 1.0 / float(blending_frame_counter + 1);
    color = mix(previous_color, color, blending);

    color = ADJUST_COLOR_OUT(color);
#endif

    imageStore(output_image, clamp(ivec2(coord), ivec2(0), ivec2(output_dimensions) - 1), color);
#endif
}
